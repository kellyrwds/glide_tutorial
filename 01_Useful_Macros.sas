
/************************************************************************
**
**               USEFUL MACROS
**
** 
**               
**
**
**
***********************************************************************/


/*IMPORT/EXPORT EXCEL TO PROJECT AS PERMANENT DATALAB TABLE*/
%LET RAWDATA = /GLIDE_RWD_Work/project/RWDS/&SYS_PROJECT./import;

%MACRO IMPORT(DATASET, FILE, SHEET);
PROC SQL; DROP TABLE _DATALAB.&DATASET; QUIT;

	PROC IMPORT
		OUT = _DATALAB.&DATASET.
		DATAFILE = "&RAWDATA./&FILE..xlsx"
		DBMS = XLSX REPLACE;
		SHEET = "&SHEET.";
	RUN;

%MEND;


/*OUTPUT*/
%LET OUTPUT = /GLIDE_RWD_Work/project/RWDS/&SYS_PROJECT./output;

%MACRO EXPORT(DATASET, FILE, SHEET);
	PROC EXPORT
		DATA = &DATASET
		OUTFILE = "&OUTPUT./&FILE..xlsx"
		DBMS = XLSX REPLACE;
		SHEET = "&SHEET.";
	RUN;
%MEND;



/*DROP TABLE MACRO - ALWAYS DROP TABLE WHEN CREATING NEW. TERADATA DOES NOT REPLACE LIKE SAS.*/
/*TEMP TERADATA VOLATILE TABLE*/
%MACRO DROPTD(OUTPUT);
PROC SQL;
%CONNECTGL;
	%IF %SYSFUNC(EXIST(_TDWORK.&OUTPUT)) %THEN %DO;
	EXECUTE(DROP TABLE &OUTPUT) BY TERADATA;
	%END;
QUIT;
%MEND;


/*PERMANENT DATALAB TABLE*/
%MACRO DROPLAB(OUTPUT);
PROC SQL;
%CONNECTGL;
	%IF %SYSFUNC(EXIST(_datalab.&OUTPUT)) %THEN %DO;
	EXECUTE(DROP TABLE &sys_datalab..&OUTPUT) BY TERADATA;
	%END;
QUIT;
%MEND;




/*TOTAL COUNT, DISTINCT COUNT, AND 100 OBSERVATION PRINT*/
%MACRO COUNT(LIB,OUTPUT,ID);
PROC CONTENTS DATA = &LIB..&OUTPUT; TITLE &OUTPUT; RUN;

PROC SQL;
TITLE &OUTPUT;
SELECT COUNT(*) FROM &LIB..&OUTPUT;
SELECT COUNT (DISTINCT &ID) FROM &LIB..&OUTPUT;
QUIT;

PROC PRINT DATA=&LIB..&OUTPUT (OBS=100); RUN;
%MEND;
